import java.text.SimpleDateFormat

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ykrc17.game.qrzdconfigeditor"
        minSdkVersion 15
        targetSdkVersion 27
        versionName new SimpleDateFormat("yyMMdd").format(new Date())
        versionCode Integer.parseInt(versionName)
        resConfigs "xxhdpi", "ldltr", "appliance"
    }
    signingConfigs {
        release {//发布版本的签名配置
            storeFile file("keystore.jks")
            keyAlias "key0"
            storePassword "123456"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    def implLibs = [fileTree(include: ["*.jar"], dir: "libs"),
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
                    "com.android.support:appcompat-v7:27.1.1",
                    "com.android.support:recyclerview-v7:27.1.1",
    ]
    implementation implLibs
}

tasks.whenTaskAdded {
    if (it.name == "packageRelease") {
        it.doLast {
            copy {
                from("$buildDir/outputs/apk/release") {
                    include "*.apk"
                    rename("app-release", "qrzd-config-editor")
                }
                into rootProject.file("release")
            }
        }
    }
}